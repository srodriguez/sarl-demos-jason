package io.sarl.demos.jason

import io.sarl.core.*
import io.sarl.util.bdi.jason.AgentArchitectureAdapter
import io.sarl.lang.core.Agent
import jason.asSemantics.Message
import java.io.Reader
import java.io.InputStreamReader
import java.io.File
import java.io.InputStream
import java.io.FileInputStream
import io.sarl.lang.core.Event
import java.util.Random

/*************************************
 * Events
 *************************************/
event KQMLMessage {
	var message : Message
}

event Perception {
	var number : Integer
}

/*************************************
 * Capacities and Skills
 *************************************/
capacity AgentSpeakReasoner {

	def reasoningCycle

	def perceive(evt : Event)

	def loadAgentSpeak(fullPath : String)
}

skill JasonAgentSpeakReasonerSkill implements AgentSpeakReasoner {
	var adapter : AgentArchitectureAdapter

	def install {
		adapter = new AgentArchitectureAdapter(owner)
	}

	def uninstall {
	/* Check with Jomi if this is how I should stop it */
		adapter.stop
		adapter = null
	}

	def reasoningCycle {
		adapter.reason
		if(!adapter.running) {
			getSkill(Lifecycle).killMe
		}
	}

	def perceive(evt : Event) {
		adapter.enqueuePerception(evt)
	}

	/* Loads an ASL file from the absolutePath
	 * This should be replaced with a more generic input (e.g. InputStream) but in seems that jason does not allow this at the moment
	 */
	def loadAgentSpeak(fullPath : String) {
		adapter.load(fullPath)
	}
}

capacity Logging {

	def debug(s : String)

	def info(s : String)
}

skill BasicConsoleLogging implements Logging {

	def debug(s : String) {
		System.out.println("DEBUG [" + owner.ID + "]: " + s)

	}

	def info(s : String) {
		System.out.println("INFO [" + owner.ID + "]: " + s)
	}
}

/*************************************
 * Agents
 *************************************/
agent BDIAgent {
	uses Schedules, AgentSpeakReasoner, Logging, Behaviors
	val random = new Random()

	on Initialize {
		val aslFilePath = occurrence.parameters.get(0) as String

		/* set the skill for logging */
		setSkill(Logging, new BasicConsoleLogging)

		/* set the reasoner implemented using Jason's Reasoning Engine*/
		setSkill(AgentSpeakReasoner, new JasonAgentSpeakReasonerSkill)

		/* Load the AgentSpeak File */
		loadAgentSpeak(aslFilePath.expandPath)

		/* Execute the reasoningCycle every 100 milliseconds */
		every(100) [reasoningCycle]
		
		/* Generate a random Perception every second */
		every(1000) [wake(randomPerception)]
	}

	on Perception {
		perceive(occurrence)
	}

	/* To map doSomething with Integer coming form the asl definition */
	def doSomething(value : Integer) {
		System.out.println("BDIAgent is doing something with parameter integer: " + value)
	}

	def expandPath(relativePath : String) : String {
		val f = new File(relativePath)
		f.absolutePath
	}

	def randomPerception : Perception {
		new Perception => [number = random.nextInt(100 - 10) + 10];
	}
}

/* 
 * This agent starts other BDI agents.
 * Here we could load mas2j files.
 */
agent BDIDemoLauncher {
	uses DefaultContextInteractions

	on Initialize {
	//		BDIAgent.spawn(#["src/main/resources/demo.asl"])
		BDIAgent.spawn(# [ "src/main/resources/demo_perceptions.asl" ])
	}
}

//behavior JasonSARLEventsMapperBehavior {
//	requires AgentSpeakReasoner
//	uses AgentSpeakReasoner
//
//	new(ag : Agent) {
//		super(ag)
//	}
//
//	on Perception {
//	}
//
////	on KQMLMessage {
////	//Make this a Message
////
////	}
//}
//
